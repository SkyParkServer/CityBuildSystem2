brig command /balance [<target:offline player>]:
    trigger:
        if {_target} is set:
            if sender is player:
                if player does not have permission "citybuild.admin.economy":
                    cbError(player, "You cannot view other player balances.")
                    stop
        else:
            if sender is not player:
                cbError(sender, "Usage: /balance <player>")
                stop
            set {_target} to player

        set {_balance} to cbBalance({_target})
        cbMessage(sender, "&e%{_target}%&7 has &6$%{_balance}%&7.")

brig command /eco <action:string> <target:offline player> <amount:int>:
    permission: citybuild.admin.economy
    arguments:
        set suggestions of arg "action" to "add", "set" and "take"
    trigger:
        if {_action} is "add":
            cbAddMoney({_target}, {_amount})
            cbSuccess(sender, "Added &6$%{_amount}%&a to &e%{_target}%&a.")
            stop

        if {_action} is "set":
            set {cb.money::%uuid of {_target}%} to {_amount}
            cbSuccess(sender, "Set balance of &e%{_target}%&a to &6$%{_amount}%&a.")
            stop

        if {_action} is "take":
            if cbTakeMoney({_target}, {_amount}) is false:
                cbError(sender, "Player has not enough money.")
                stop
            cbSuccess(sender, "Removed &6$%{_amount}%&a from &e%{_target}%&a.")
            stop

        cbError(sender, "Usage: /eco <add|set|take> <player> <amount>")
