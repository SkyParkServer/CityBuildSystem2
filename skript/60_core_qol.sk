brig command /workbench:
    aliases: wb, craft
    permission: cb.workbench.use
    trigger:
        if sender is not player:
            cbError(sender, "Only players can run this command.")
            stop

        open workbench for player

brig command /anvil:
    aliases: amboss
    permission: cb.anvil.use
    trigger:
        if sender is not player:
            cbError(sender, "Only players can run this command.")
            stop

        open anvil for player

brig command /sign:
    aliases: unterschrift, unterschift
    permission: cb.sign.use
    trigger:
        if sender is not player:
            cbError(sender, "Only players can run this command.")
            stop

        if player's tool is air:
            cbError(player, "Hold an item in your hand first.")
            stop

        set {_item} to player's tool
        set {_lore::*} to lore of {_item}

        set {_signedByAny} to false
        set {_signedByPlayer} to false
        set {_signaturePrefix} to cbColor("&7Signiert von &b")
        set {_signatureByPlayerPrefix} to cbColor("&7Signiert von &b%player% &7am &b")

        loop {_lore::*}:
            if loop-value starts with {_signaturePrefix}:
                set {_signedByAny} to true
            if loop-value starts with {_signatureByPlayerPrefix}:
                set {_signedByPlayer} to true

        if {_signedByPlayer} is true:
            cbError(player, "You already signed this item.")
            stop

        if {_signedByAny} is true:
            if player does not have permission "cb.sign.team":
                cbError(player, "This item is already signed.")
                stop

        set {_dateText} to now formatted as "dd.MM.yyyy HH:mm"
        add "" to {_lore::*}
        add cbColor("&7Signiert von &b%player% &7am &b%{_dateText}%") to {_lore::*}
        set lore of {_item} to {_lore::*}
        set player's tool to {_item}

        cbSuccess(player, "Item signed.")

brig command /repair:
    permission: cb.repair.use
    trigger:
        if sender is not player:
            cbError(sender, "Only players can run this command.")
            stop

        if player's tool is air:
            cbError(player, "Hold an item in your hand first.")
            stop

        repair tool of player
        cbSuccess(player, "Your item has been fully repaired.")

brig command /wetter [<mode:string>]:
    permission: cb.wetter.use
    arguments:
        set suggestions of arg "mode" to "regen", "gewitter" and "sonne"
    trigger:
        if sender is not player:
            cbError(sender, "Only players can run this command.")
            stop

        if {_mode} is not set:
            cbError(player, "Usage: /wetter <regen|gewitter|sonne>")
            stop

        if {_mode} is "regen":
            set weather of world of player to rainy
            cbSuccess(player, "Wetter gesetzt: Regen.")
            stop

        if {_mode} is "gewitter":
            set weather of world of player to thundering
            cbSuccess(player, "Wetter gesetzt: Gewitter.")
            stop

        if {_mode} is "sonne":
            set weather of world of player to sunny
            cbSuccess(player, "Wetter gesetzt: Sonne.")
            stop

        cbError(player, "Usage: /wetter <regen|gewitter|sonne>")
