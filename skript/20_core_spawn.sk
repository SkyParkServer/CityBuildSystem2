brig command /setspawn:
    permission: citybuild.admin.spawn
    trigger:
        if sender is not player:
            cbError(sender, "Only players can run this command.")
            stop

        set {cb.spawn.location} to location of player
        cbSuccess(player, "Spawn has been updated.")
        cbDebug("Spawn changed by %player% to %cbLocToText(location of player)%")

brig command /spawn:
    trigger:
        if sender is not player:
            cbError(sender, "Only players can run this command.")
            stop

        if {cb.spawn.location} is not set:
            cbError(player, "Spawn is not configured yet. Use /setspawn first.")
            stop

        if cbHasCooldown("spawn", player) is true:
            set {_remaining} to cbCooldownRemaining("spawn", player)
            cbError(player, "You can use /spawn again in %{_remaining}%.")
            stop

        set {_cooldownSeconds} to {cb.spawn.cooldown-seconds}
        if {_cooldownSeconds} is not set:
            set {_cooldownSeconds} to {cb.const.default-spawn-cooldown-seconds}

        cbStartCooldown("spawn", player, {_cooldownSeconds})
        teleport player to {cb.spawn.location}
        cbSuccess(player, "Teleported to spawn.")
